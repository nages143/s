trigger changeLog on Engineering_Technical_Request__c (after update) {
    
    final List<Schema.FieldSetMember> trackedFields = sObjectType.Engineering_Technical_Request__c.FieldSets.FieldTracking.getFields();
    if (trackedFields.isEmpty()) return;
    
    final List<ETR_Change_Log__c> fieldChanges = new List<ETR_Change_Log__c>();
    
   // if(!trigger.isUpdate)
     //   return;
    
    for (Engineering_Technical_Request__c newETR : trigger.new) {
        
        final Engineering_Technical_Request__c oldETR = trigger.oldmap.get(newETR.Id);
        
        for (Schema.FieldSetMember fsm : trackedFields) {
            
            String fieldName  = fsm.getFieldPath();
            String fieldLabel = fsm.getLabel();
            if (newETR.get(fieldName) == oldETR.get(fieldName))
                continue;
            String oldValue = String.valueOf(oldETR.get(fieldName));
            String newValue = String.valueOf(newETR.get(fieldName));
            
            if (oldValue != null && oldValue.length()>255) 
                oldValue = oldValue.substring(0,255);
            
            if (newValue != null && newValue.length()>255) 
                newValue = newValue.substring(0,255); 
            
            
            final ETR_Change_Log__c ETRHistory = new ETR_Change_Log__c();
            
            ETRHistory.Engineering_Technical_Request__c = newETR.Id;
            ETRHistory.Field_Label__c = fieldLabel;
            ETRHistory.apiname__c = fieldName;
            ETRHistory.ChangedBy__c = UserInfo.getName();
            ETRHistory.OldValue__c  = oldValue;
            ETRHistory.NewValue__c  = newValue;
            ETRHistory.Date_Changed__c = newETR.LastModifiedDate;
            fieldChanges.add(ETRHistory);
            
        }
        
    }
    
    if (!fieldChanges.isEmpty()) {
        insert fieldChanges;
    }
    
    
}
